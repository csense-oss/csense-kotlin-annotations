import org.jetbrains.kotlin.gradle.dsl.ExplicitApiMode
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {

    ext.compileJavaVersion = "11"

    ext.kotlin_version = "2.1.21"
    //versions & naming
    ext.csenseVersionName = "0.1.0"
    ext.csenseGroupId = "org.csenseoss.kotlin"
    ext.csenseArtifactId = "csense-kotlin-annotations"

    //repo settings
    ext.csenseUserOrg = "csense-oss"
    ext.csenseRepoName = "maven"

    ext.csenseGithub = "csense-oss/csense-kotlin-annotations"
    ext.csenseLicense = "MIT"
    ext.csenseLicenseUrl = "https://mit-license.org/"

    ext.csenseDeveloperAlias = "tvede-dk"
    ext.csenseDeveloperName = "Kasper Tvede"
    ext.csenseDeveloperEmail = "csenseoss@tvedesys.dk"

    ext.readme = "readme.md"
    ext.csenseWebsite = "https://github.com/csense-oss/csense-kotlin-annotations"
    ext.csenseGit = "https://github.com/csense-oss/csense-kotlin-annotations.git"
    ext.csenseArtifactDescription = "An annotations based library that together with a plugin provides advanced IDE support / help"
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version "$kotlin_version"
    id "maven-publish"
    //https://github.com/vanniktech/gradle-maven-publish-plugin
    id "com.vanniktech.maven.publish" version "0.32.0"
}

repositories {
    mavenCentral()
}

project.group = csenseGroupId
project.version = csenseVersionName

group project.csenseGroupId
version project.csenseVersionName

kotlin {
    explicitApi = ExplicitApiMode.Strict

    jvm {
        compilations.configureEach {
            kotlinOptions.jvmTarget = compileJavaVersion
        }
    }
    js(IR) {
        browser()
        nodejs()
    }

    //Native targets, based on https://kotlinlang.org/docs/native-target-support.html
    //- tier 1
    macosX64()
    macosArm64()
    iosSimulatorArm64()
    iosX64()

    //- tier 2
    linuxX64()
    linuxArm64()
    watchosSimulatorArm64()
    watchosX64()
    watchosArm32()
    watchosArm64()
    tvosSimulatorArm64()
    tvosX64()
    tvosArm64()
    iosArm64()

    //- tier 3
    androidNativeArm32()
    androidNativeArm64()
    androidNativeX86()
    androidNativeX64()
    mingwX64()
    androidNativeX64()
    watchosDeviceArm64()
    //end native targets

    sourceSets {
        commonMain {
        }
    }

}





task uploadProd(dependsOn: ["assemble", "allTests", "publish"])

// warnings as errors
tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.allWarningsAsErrors = true
}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(compileJavaVersion))
    }
}


//progressive mode & api version + optIns
kotlin.sourceSets.configureEach {
    languageSettings {
        progressiveMode = true
        optIn("kotlin.contracts.ExperimentalContracts")
    }
}


mavenPublishing {
    coordinates(csenseGroupId, csenseArtifactId, csenseVersionName)
}

apply from: "$projectDir/gradle/publish.gradle"