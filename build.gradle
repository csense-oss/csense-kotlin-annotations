buildscript {
    //versions & naming
    ext.csenseVersionName = "0.0.19"
    ext.csenseGroupId = "csense.kotlin"
    ext.csenseArtifactId = "csense-kotlin-annotations"

    //repo settings
    ext.csenseUserOrg = "csense-oss"
    ext.csenseRepoName = "maven"

    ext.csenseGithub = "csense-oss/csense-kotlin-annotations"
    ext.csenseLicense = "MIT"
    ext.readme = "readme.md"
    ext.csenseWebsite = "https://github.com/csense-oss/csense-kotlin-annotations"
    ext.csenseGit = "https://github.com/csense-oss/csense-kotlin-annotations.git"
    ext.csenseArtifactDescription = "An annotations based library that together with a plugin provides advanced IDE support / help"
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version "1.4.0"
    id("org.jetbrains.dokka") version "0.10.1"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}


repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url 'https://kotlin.bintray.com/kotlinx' }
    maven { url "https://dl.bintray.com/csense-oss/maven" }
}

group project.csenseGroupId
version project.csenseVersionName

kotlin {
    explicitApi = 'strict'
    jvm()
    js {
        browser {
        }
        nodejs {
        }
    }
    sourceSets {
        commonMain {
        }
        jvmMain {

        }

        jsMain {
        }

    }
}

kotlin.sourceSets.all {
    languageSettings.progressiveMode = true
}

apply from: "publish.gradle"
dokka {
    multiplatform {
        global {
            perPackageOption {
                prefix = "csense.kotlin.annotations"
                suppress = true
            }
        }
        js {}
        jvm {}
    }
}


task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = "$buildDir/dokkaJavadoc"
}

final def javaDocJarTask = task("javadocJar", type: Jar, dependsOn: "dokkaJavadoc") {
    archiveClassifier.set('javadoc')
    from "${project.buildDir}/dokkaJavadoc"
}

publish.dependsOn(javaDocJarTask)